InlinerEliminationStaticTest
---
InlineFunctionCalls
Simplify
BeginInlinedFunctionElimination
---
Simple
---
def foo():
    # Chosen by fair dice roll. Guaranteed to be random.
    return 4

def test():
    return foo()
---
fun jittestmodule:test {
  bb 0 {
    v2:MortalFunc[function:0xdeadbeef] = LoadConst<MortalFunc[function:0xdeadbeef]>
    v6:ImmortalLongExact[4] = LoadConst<ImmortalLongExact[4]>
    Return v6
  }
}
---
InlineMultipleFunctions
---
def foo():
    return 3

def bar():
    return 4

def test():
    return foo() + bar()
---
fun jittestmodule:test {
  bb 0 {
    v5:MortalFunc[function:0xdeadbeef] = LoadConst<MortalFunc[function:0xdeadbeef]>
    v12:ImmortalLongExact[3] = LoadConst<ImmortalLongExact[3]>
    v7:MortalFunc[function:0xdeadbeef] = LoadConst<MortalFunc[function:0xdeadbeef]>
    v16:ImmortalLongExact[4] = LoadConst<ImmortalLongExact[4]>
    UseType<LongExact> v12
    UseType<LongExact> v16
    UseType<ImmortalLongExact[3]> v12
    UseType<ImmortalLongExact[4]> v16
    v19:ImmortalLongExact[7] = LoadConst<ImmortalLongExact[7]>
    Return v19
  }
}
---
BeginEndWithMemoryEffectsNotRemoved
---
def add(x, y):
  return x + y

def test():
    return add(3, "x")
---
fun jittestmodule:test {
  bb 0 {
    v4:ImmortalLongExact[3] = LoadConst<ImmortalLongExact[3]>
    v5:MortalUnicodeExact["x"] = LoadConst<MortalUnicodeExact["x"]>
    v6:MortalFunc[function:0xdeadbeef] = LoadConst<MortalFunc[function:0xdeadbeef]>
    BeginInlinedFunction<jittestmodule:add> {
      NextInstrOffset 10
    }
    v16:Object = BinaryOp<Add> v4 v5 {
      FrameState {
        NextInstrOffset 8
        Locals<2> v4 v5
      }
    }
    EndInlinedFunction
    Return v16
  }
}
---
InlinedStoreFieldLoadFieldIsEliminated
---
from __static__ import int8

class C:
  def __init__(self):
    self.foo: int8 = 4

  def getfoo(self) -> int8:
    return self.foo

def test() -> int8:
    return C().getfoo()
---
fun jittestmodule:test {
  bb 0 {
    v5:ObjectUser[C:Exact] = TpAlloc<C> {
      FrameState {
        NextInstrOffset 4
      }
    }
    v6:MortalFunc[function:0xdeadbeef] = LoadConst<MortalFunc[function:0xdeadbeef]>
    v17:CInt8[4] = LoadConst<CInt8[4]>
    v19:Nullptr = LoadConst<Nullptr>
    UseType<CInt8> v17
    StoreField<foo@16> v5 v17 v19
    v21:NoneType = LoadConst<NoneType>
    v8:MortalFunc[function:0xdeadbeef] = LoadConst<MortalFunc[function:0xdeadbeef]>
    v28:CInt8 = LoadField<foo@16, CInt8, borrowed> v5
    Return<CInt8> v28
  }
}
---
