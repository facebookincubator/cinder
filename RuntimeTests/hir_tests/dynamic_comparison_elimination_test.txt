DynamicComparisonEliminationTest
---
DynamicComparisonElimination
---
IsBecomesComparison
---
# HIR
fun test {
  bb 0 {
    v0 = LoadArg<0>
    v1 = LoadArg<1>
    v2 = Compare<Is> v0 v1
    v3 = IsTruthy v2
    CondBranch<1, 2> v3
  }

  bb 1 {
    v4 = LoadConst<LongExact[1]>
    Return v4
  }

  bb 2 {
    v5 = LoadConst<NoneType>
    Return v5
  }
}
---
fun test {
  bb 0 {
    v0:Object = LoadArg<0>
    v1:Object = LoadArg<1>
    v3:CBool = PrimitiveCompare<Equal> v0 v1
    CondBranch<1, 2> v3
  }

  bb 1 (preds 0) {
    v4:ImmortalLongExact[1] = LoadConst<ImmortalLongExact[1]>
    Return v4
  }

  bb 2 (preds 0) {
    v5:NoneType = LoadConst<NoneType>
    Return v5
  }
}
---
IsNotBecomesComparison
---
# HIR
fun test {
  bb 0 {
    v0 = LoadArg<0>
    v1 = LoadArg<1>
    v2 = Compare<IsNot> v0 v1
    v3 = IsTruthy v2
    CondBranch<1, 2> v3
  }

  bb 1 {
    v4 = LoadConst<LongExact[1]>
    Return v4
  }

  bb 2 {
    v5 = LoadConst<NoneType>
    Return v5
  }
}
---
fun test {
  bb 0 {
    v0:Object = LoadArg<0>
    v1:Object = LoadArg<1>
    v3:CBool = PrimitiveCompare<NotEqual> v0 v1
    CondBranch<1, 2> v3
  }

  bb 1 (preds 0) {
    v4:ImmortalLongExact[1] = LoadConst<ImmortalLongExact[1]>
    Return v4
  }

  bb 2 (preds 0) {
    v5:NoneType = LoadConst<NoneType>
    Return v5
  }
}
---
IsInstanceFastpath
---
def test(x):
  if isinstance(x, str):
    return 1
---
fun jittestmodule:test {
  bb 0 {
    v7:Object = LoadArg<0; "x">
    v8:OptObject = LoadGlobalCached<0; "isinstance">
    v9:MortalObjectUser[builtin_function_or_method:isinstance:0xdeadbeef] = GuardIs<0xdeadbeef> v8 {
      Descr 'LOAD_GLOBAL: isinstance'
    }
    v11:OptObject = LoadGlobalCached<1; "str">
    v12:ImmortalTypeExact[str:obj] = GuardIs<0xdeadbeef> v11 {
      Descr 'LOAD_GLOBAL: str'
    }
    v17:Type = LoadField<ob_type@8, Type, borrowed> v7
    v18:CBool = PrimitiveCompare<Equal> v17 v12
    CondBranch<1, 3> v18
  }

  bb 3 (preds 0) {
    v14:CInt32 = IsInstance v7 v12 {
      FrameState {
        NextInstrOffset 10
        Locals<1> v7
      }
    }
    CondBranch<1, 2> v14
  }

  bb 1 (preds 0, 3) {
    v15:ImmortalLongExact[1] = LoadConst<ImmortalLongExact[1]>
    Return v15
  }

  bb 2 (preds 3) {
    v16:NoneType = LoadConst<NoneType>
    Return v16
  }
}
---
